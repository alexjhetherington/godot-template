[gd_resource type="Shader" format=3 uid="uid://b8gphl77fmc77"]

[resource]
code = "//*** *** *** *** *** *** *** *** *** *** ***    ***//
//*** ===  PSX lit surface shader effect     === ***//
//*** ===          For Godot 4.0             === ***//
//*** ===         By Analog Feelings         === ***//
//*** === https://github.com/AnalogFeelings  === ***//
//*** *** *** *** *** *** *** *** *** *** ***    ***//
//*** ===       Licensed under the:          === ***//
//*** ===           MIT License              === ***//
//*** *** *** *** *** *** *** *** *** *** ***    ***//
//*** ===    Derived from CC0 work by:       === ***//
//*** ===           Mighty Duke              === ***//
//*** *** *** *** *** *** *** *** *** *** ***    ***//

/*****************************************************************************
MIT License

Copyright (c) 2022 AnalogFeelings

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the \"Software\"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*****************************************************************************/

shader_type spatial;
render_mode skip_vertex_transform,
//			vertex_lighting,
//			blend_mix, depth_draw_opaque,
			specular_disabled, diffuse_lambert_wrap; // Irrelevant with custom light func

uniform sampler2D albedo: hint_default_black, filter_nearest_mipmap;
uniform sampler2D specular: hint_default_black, filter_nearest;
uniform sampler2D emission: hint_default_black, filter_nearest;

// The \"jumpiness\" of vertices.
uniform float jitter: hint_range(0.0, 100.0) = 20;

// If disabled, jitter will not affect the Z coordinate
// of the vertex.
uniform bool jitter_z_coordinate = true;

// If disabled, makes vertices further away from the camera
// less jittery.
uniform bool jitter_depth_independent = true;

// Enables the PS1 texture warping.
uniform bool affine_texture_mapping = true;

// Any alpha value below this will not be rendered.
uniform float alpha_scissor: hint_range(0.0, 1.0) = 1.0;

// Color to overlay on top of the albedo texture.
uniform vec4 albedo_color: source_color = vec4(1.0);

// Emission color.
uniform vec4 emission_color: source_color = vec4(1.0);

// Texture tiling.
uniform vec2 texture_size = vec2(1.0);

uniform float mip_map_far: hint_range(1.0, 2000.0) = 60;
uniform int mip_map_final_level: hint_range(0, 7) = 6;

uniform vec4 material_type: source_color = vec4(0.0);

bool is_material_camera(uint camera_visible_layers) {
	return (int(camera_visible_layers) & (1 << 1)) == 0;
}

void vertex() {
	VERTEX = (MODELVIEW_MATRIX * vec4(VERTEX, 1.0)).xyz;
	NORMAL = (MODELVIEW_MATRIX * vec4(NORMAL, 0.0)).xyz;

	float z_orig = VERTEX.z;
	float i = (100.0 - jitter);

	if (jitter_depth_independent) {
		float w = (PROJECTION_MATRIX * vec4(VERTEX, 1.0)).w;
		VERTEX = round(VERTEX / w * i) / i * w;
	} else {
		VERTEX = round(VERTEX * i) / i;
	}

	if (!jitter_z_coordinate) {
		VERTEX.z = z_orig;
	}

	if (affine_texture_mapping) {
		UV *= VERTEX.z;
	}
}

// Passing it to the light shader
varying uint camera_visible_layers;
void fragment() {
	vec2 uv = UV;
	ALPHA = texture(albedo, uv).a;
	ALPHA_SCISSOR_THRESHOLD = alpha_scissor;

	camera_visible_layers = CAMERA_VISIBLE_LAYERS;
	if (is_material_camera(camera_visible_layers)) {
		ALBEDO = material_type.xyz;
	}
	else {
		if (affine_texture_mapping) {
			uv /= VERTEX.z;
		}

		uv /= texture_size;

		// getting the raw none linear depth value
	    float depth_raw = FRAGCOORD.z;

	    // make it linear
	    vec3 ndc = vec3(SCREEN_UV * 2.0 - 1.0, depth_raw);
	    vec4 view = INV_PROJECTION_MATRIX * vec4(ndc, 1.0);
	    view.xyz /= view.w;

	    // reverse
	    float depth_linear = -view.z;

	    // normalize
		const float cam_near = 2.2;
		// (depth_linear - cam_near) / (cam_far - cam_near)
	    float depth_normalized = min(1.0, (depth_linear - cam_near) / (mip_map_far - cam_near)); //Something

		// Smooth out falloff
		depth_normalized = 1.0 - (depth_normalized - 1.0) * (depth_normalized - 1.0);

	    // show as grey scale
	    //ALBEDO.rgb = vec3(depth_normalized);
		// Overlay albedo color with the albedo texture.
		//ALBEDO = albedo_color.rgb * texture(albedo, uv).rgb;
		//ALBEDO = albedo_color.rgb * textureLod(albedo, UV, 0).rgb;
		ALBEDO = albedo_color.rgb * textureLod(albedo, uv, depth_normalized * float(mip_map_final_level)).rgb;

		EMISSION = emission_color.rgb * texture(emission, uv).rgb;
		SPECULAR = texture(specular, uv).r;
	}
}

// See https://github.com/godotengine/godot/blob/master/drivers/gles3/shaders/scene.glsl
void light() {
	if (is_material_camera(camera_visible_layers)) {
		DIFFUSE_LIGHT = vec3(1, 1, 1);
	}
	else{
		float NdotL = dot(NORMAL, LIGHT);

		float diffuse_brdf_NL = max(0, (NdotL + ROUGHNESS) / ((1.0 + ROUGHNESS) * (1.0 + ROUGHNESS))) * (1.0 / PI);

		DIFFUSE_LIGHT += LIGHT_COLOR * diffuse_brdf_NL * ATTENUATION;
	}
}
"
